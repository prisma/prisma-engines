// ######################################
// Readme
// ######################################
// You will notice the pattern ( X ~ Y | X )
// instead of X ~ Y?. This is because we do not
// want the concat operator ~ between X and Y
// to consume any whitespace after X, if Y is not present.

// This is how PEG grammars work:
// https://pest.rs/book/grammars/peg.html

// This is the basic syntax of Pest grammar files:
// https://pest.rs/book/grammars/syntax.html#cheat-sheet

// ######################################
// Schema - the root of all rules
// ######################################
schema = { SOI ~ NEWLINE* ~ (model_declaration | enum_declaration | config_block | type_alias | arbitrary_block | comment_block | NEWLINE | CATCH_ALL)* ~ EOI }

// ######################################
// Model and composite types
// ######################################

// At the syntax level, models and composite types are the same.
model_declaration = { comment_block? ~ (MODEL_KEYWORD | TYPE_KEYWORD) ~ non_empty_identifier ~ BLOCK_OPEN ~ ( field_declaration | ( block_level_attribute ) | NEWLINE | doc_comment_and_new_line | comment_and_new_line | BLOCK_LEVEL_CATCH_ALL )* ~ BLOCK_CLOSE }

field_declaration = { doc_comment_and_new_line* ~ non_empty_identifier ~ LEGACY_COLON? ~ (field_type ~ ( "@" ~ attribute )+ | field_type)? ~ doc_comment? ~ comment? ~ NEWLINE }


// ######################################
// Field Type
// ######################################

// Pest is greedy, order is very important here.
field_type = { unsupported_optional_list_type | list_type | optional_type | legacy_required_type | legacy_list_type | base_type  }

unsupported_type = { "Unsupported(" ~ string_literal ~ ")" }
base_type = { unsupported_type | non_empty_identifier } // Called base type to not conflict with type rust keyword
unsupported_optional_list_type = { base_type ~ "[]" ~ "?" }
list_type = { base_type ~ "[]" }
optional_type = { base_type ~ "?" }
legacy_required_type = { non_empty_identifier ~ "!" }
legacy_list_type = { "[" ~ non_empty_identifier ~ "]" }

// ######################################
// Type Alias
// ######################################
type_alias = { comment_block? ~ TYPE_KEYWORD ~ non_empty_identifier ~ "=" ~ (base_type ~ ( "@" ~ attribute )+ | base_type)  }

// ######################################
// Configuration blocks
// ######################################
config_block = { comment_block? ~ (DATASOURCE_KEYWORD | GENERATOR_KEYWORD) ~ non_empty_identifier ~ BLOCK_OPEN ~ (key_value | doc_comment_and_new_line | comment_and_new_line | NEWLINE | BLOCK_LEVEL_CATCH_ALL)* ~ BLOCK_CLOSE }
key_value = { non_empty_identifier ~ "=" ~ expression ~ NEWLINE }

// a block definition without a keyword. Is not valid. Just acts as a catch for the parser to display a nice error.
arbitrary_block = { comment_block? ~ non_empty_identifier ~ BLOCK_OPEN ~ ((!BLOCK_CLOSE ~ ANY) | NEWLINE)* ~ BLOCK_CLOSE }

// ######################################
// Enum
// ######################################
enum_declaration = { comment_block? ~ ENUM_KEYWORD ~ non_empty_identifier ~ BLOCK_OPEN ~ (enum_value_declaration | block_level_attribute | NEWLINE | doc_comment_and_new_line | comment_and_new_line | BLOCK_LEVEL_CATCH_ALL)* ~ BLOCK_CLOSE }
enum_value_declaration = { doc_comment_and_new_line* ~ (non_empty_identifier ~ ( "@" ~ attribute )+ | non_empty_identifier) ~ doc_comment? ~ comment? ~ NEWLINE }

// ######################################
// Attributes
// ######################################
block_level_attribute = { "@@" ~ attribute ~ doc_comment? ~ comment? ~ NEWLINE }
// An attribute may have no arguments at all.
attribute = { (attribute_name ~ arguments_list | attribute_name) }
// This is a poor-mans version of name spacing. This is currently used for native types.
attribute_name = @{ (maybe_empty_identifier ~ ".")? ~ maybe_empty_identifier }

// ######################################
// Arguments
// ######################################
arguments_list = { "(" ~ (argument ~ ("," ~ argument)*)? ~ trailing_comma? ~ ")" }
argument = _{ named_argument | empty_argument | expression }
empty_argument = { argument_name ~ ":" }
named_argument = { argument_name ~ ":" ~ expression }
argument_name = { non_empty_identifier }
trailing_comma = @{ "," }

// ######################################
// Comments and Documentation Comments
// ######################################
comment_block = ${ (doc_comment_and_new_line | doc_comment | comment_and_new_line | comment) + }
doc_comment_and_new_line = ${ doc_comment ~ NEWLINE }
doc_comment = { WHITESPACE* ~ "///" ~ doc_content }
comment_and_new_line = ${ comment ~ NEWLINE }
comment = { WHITESPACE* ~ (!"///") ~ "//" ~ doc_content }
doc_content = @{ (!NEWLINE ~ ANY)* }

// ######################################
// shared building blocks
// ######################################
maybe_empty_identifier = @{ ( "_" | "-" | ASCII_ALPHANUMERIC)* }
non_empty_identifier = @{ ASCII_ALPHANUMERIC ~ maybe_empty_identifier }
path_identifier = @{ ASCII_ALPHANUMERIC ~ maybe_empty_identifier ~ ("." ~ non_empty_identifier)* }

WHITESPACE = @{ SPACE_SEPARATOR | "\t" } // tabs are also whitespace
NEWLINE = { "\n" | "\r\n" | "\r" }

// the any part is to not crash on comments next to an open block, see test `parse_comments_without_crasing_or_loosing_info`
BLOCK_OPEN = { "{" ~ (!NEWLINE ~ ANY)* ~ NEWLINE }
BLOCK_CLOSE = { "}" }

// those rules are silent because we don't want to handle the tokens
ENUM_KEYWORD = _{ "enum" }

// rules that we want to handle explicitly
MODEL_KEYWORD = { "model" }
TYPE_KEYWORD = { "type" }
GENERATOR_KEYWORD = { "generator" }
DATASOURCE_KEYWORD = { "datasource" }
LEGACY_COLON = { ":" }

CATCH_ALL = { (!NEWLINE ~ ANY)+ ~ NEWLINE? }
BLOCK_LEVEL_CATCH_ALL = { !BLOCK_CLOSE ~ CATCH_ALL }

// ######################################
// Expressions & Functions
// ######################################
function_name = @{ non_empty_identifier ~ ("." ~ non_empty_identifier)* }
function = { function_name ~ arguments_list }
array_expression = { "[" ~ (expression ~ ( "," ~ expression )*)? ~ "]" }
expression = { function | array_expression | numeric_literal | string_literal | constant_literal }

// ######################################
// Literals / Values
// ######################################
number = @{ ASCII_DIGIT+ }

numeric_literal = @{ ("-")? ~ ASCII_DIGIT+ ~("." ~ ASCII_DIGIT+)? }

// String literals with support for escaped characters.
string_escaped_predefined = { "n" | "r" | "t" | "\\" | "0" | "\"" | "'" | SPACE_SEPARATOR }
string_escape = { "\\" ~ string_escaped_predefined }
// This is only used to escape in the parser. The string above is still treated as atomic.
string_raw = { (!("\\" | "\"" | NEWLINE ) ~ ANY)+ }
string_content = ${ (string_raw | string_escape )* }
string_literal = { "\"" ~ string_content ~ "\"" }

constant_literal = @{ path_identifier }
